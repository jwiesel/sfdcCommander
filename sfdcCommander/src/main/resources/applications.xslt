<?xml version="1.0" encoding="UTF-8"?>

<xsl:stylesheet version="1.0"
xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
xmlns:sfdc="http://soap.sforce.com/2006/04/metadata">

	<xsl:output
		method="html"
		doctype-public="-//W3C//DTD XHTML 1.0 Transitional//EN"
		omit-xml-declaration="yes"
		encoding="UTF-8"
		indent="yes" />
		
	<!-- Highlight Description template -->
	<xsl:template name="highlight-description">
		<xsl:param name="field" />
		<xsl:choose>
			<xsl:when test="string-length($field)> 24">
				<xsl:attribute name="class">gooddocu</xsl:attribute><xsl:value-of select="$field" />
			</xsl:when>
			<xsl:when test="string-length($field)> 0">
				<xsl:attribute name="class">weakdocu</xsl:attribute>Please update the description: <xsl:value-of select="$field" />
			</xsl:when>
			<xsl:otherwise>
				<xsl:attribute name="class">nodocu</xsl:attribute>Please add a description!
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
	
	<!-- Substring after Last Template -->
	<xsl:template name="substring-after-last">
		<xsl:param name="string" />
		<xsl:param name="delimiter" />
		<xsl:choose>
			<xsl:when test="contains($string, $delimiter)">
				<xsl:call-template name="substring-after-last">
					<xsl:with-param name="string" select="substring-after($string, $delimiter)" />
					<xsl:with-param name="delimiter" select="$delimiter" />
				</xsl:call-template>
			</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="$string" />
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template match="/">
		<xsl:variable name="file"><xsl:call-template name="substring-after-last"><xsl:with-param name="string" select="base-uri()" /><xsl:with-param name="delimiter" select="'/'" /></xsl:call-template></xsl:variable>
		<html>
			<head>
				<title>Salesforce.com Application Documentation: <xsl:value-of select="$file"/></title>
				<script type="text/javascript" src="../script/script.js"></script>
				<script type="text/javascript" src="../script/jquery-1.8.3.min.js"></script>
				<script>
				$(document).ready(function(){init();});
				</script>
				<link rel="stylesheet" href="../css/styles.css" type="text/css" />
			</head>
			<body>
				<h1 class="pageheadline">Salesforce.com System Documentation</h1>
				<xsl:for-each select="sfdc:CustomApplication">
					<h2><xsl:value-of select="$file"/></h2>
					<p>Generated by sfdcCommander TEST on <xsl:value-of select="format-dateTime(current-dateTime(),'[Y0001]-[M01]-[D01] [H]:[m]:[s]')"/></p>
					
					<p>
						<ul class="quicknav">
							<h3>Quick Navigation</h3>
							<li><a href="../lists/applications.html"><img src="../images/back.png"/> Back to Application List</a></li>
							<!-- <li><a href="../workflows/">Workflows</a></li> -->
						</ul>
					</p>
									
					<p>
						<b>Label: </b><xsl:value-of select="sfdc:label"/><br />
						<b>Description: </b><span><xsl:call-template name="highlight-description"><xsl:with-param name="field" select="sfdc:description" /></xsl:call-template></span><br />
						<b>Default Landing Tab: </b><xsl:value-of select="sfdc:defaultLandingTab"/><br />
						<xsl:if test="count(sfdc:logo)>0">
							<b>Logo:</b><br />
							<img><xsl:attribute name="src"><xsl:value-of select='concat("../", sfdc:logo)' /></xsl:attribute></img>
						</xsl:if>
					</p>
					
					<xsl:if test="count(sfdc:tab)>0">
						<h3><a name="tab">Tabs</a></h3>
						<table class="datatable">
							<tr class="headline">
								<th>Tab</th>
							</tr>
							<xsl:for-each select="sfdc:tab">
								<tr class="dataline">
									<td>
										<xsl:choose>
											<xsl:when test="starts-with(.,'standard-')">
												<xsl:value-of select="."/>
											</xsl:when>
											<xsl:otherwise>
												<a><xsl:attribute name="href"><xsl:value-of select="concat('../tabs/', ., '.html')"/></xsl:attribute><xsl:value-of select="."/></a>
											</xsl:otherwise>
										</xsl:choose>
									</td>
								</tr>
							</xsl:for-each>
						</table>
					</xsl:if>
					
				</xsl:for-each>
			</body>
		</html>
	</xsl:template>

</xsl:stylesheet>
